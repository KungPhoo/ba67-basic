1 IF -3+4*(-2)-(-(-5))/(3+-1)<>-13.5 THEN PRINT "ERROR: COMPLEX MATH 1"
10 PRINT "COMMODORE 64 BASIC V2 TEST PROGRAM"
20 PRINT "  TESTING NUMERIC OPERATIONS..."
30 IF (2+2)<>(4) THEN PRINT "ERROR: ADDITION"
40 IF (10-3)<>(7) THEN PRINT "ERROR: SUBTRACTION"
50 IF (5*5)<>(25) THEN PRINT "ERROR: MULTIPLICATION"
60 IF (20/4)<>(5) THEN PRINT "ERROR: DIVISION"
70 IF ABS(100-10^2)>0.001 THEN PRINT "ERROR: EXPONENTIATION"
71 IF INT(4.9)<>4 THEN PRINT "ERROR: INT"
72 IF VAL(STR$(4.9))<>4.9 THEN PRINT "ERROR: VAL, STR$"
74 IF -3+4*(-2)-(-(-5))/(3+-1)<>-13.5 THEN PRINT "ERROR: COMPLEX MATH 1"
75 IF -3+4*(-2)-(-(-5))/(3+-1)=-13.5 THEN PRINT "  PASS COMPLEX MATH 2"
80 PRINT "  NUMERIC OPERATIONS PASSED."

90 PRINT "  TESTING STRING OPERATIONS..."
100 IF ("HELLO"+" WORLD")<>"HELLO WORLD" THEN PRINT "ERROR: STRING CONCATENATION"
110 A$="C64"
120 IF (LEN(A$))<>(3) THEN PRINT "ERROR: LEN()"
130 IF (LEFT$(A$,2))<>"C6" THEN PRINT "ERROR: LEFT$()"
140 IF (RIGHT$(A$,2))<>"64" THEN PRINT "ERROR: RIGHT$()"
150 IF (MID$(A$,2,1))<>"6" THEN PRINT "ERROR: MID$()"
160 PRINT "  STRING OPERATIONS PASSED."

170 PRINT "  TESTING VARIABLES AND ASSIGNMENTS..."
180 A=10: B=5: C=(A+B)
190 IF (C)<>(15) THEN PRINT "ERROR: VARIABLE ASSIGNMENT"
200 PRINT "  VARIABLE ASSIGNMENTS PASSED."

201 PRINT "  TESTING DIM..."
202 DIM ARR(4,5)
203 FOR X = 1 TO 4: FOR Y = 1 TO 5
204 ARR(X,Y) = X+Y
205 NEXT Y:NEXT X
206 IF ARR(2,3) <> 2+3 THEN PRINT "ERROR: DIM"
208 PRINT "  DIM PASSED, IF 5 = ";INT(ARR(2,3))


210 PRINT "  TESTING CONDITIONALS..."
220 IF (1=0) THEN PRINT "ERROR: EQUALITY TEST"
230 IF (5<3) THEN PRINT "ERROR: LESS THAN TEST"
240 IF (10>20) THEN PRINT "ERROR: GREATER THAN TEST"
250 PRINT "  CONDITIONALS PASSED."

260 PRINT "  TESTING LOOPS..."
270 S=0
280 FOR I=1 TO 10: S=S+I: NEXT I
290 IF (S)<>(55) THEN PRINT "ERROR: FOR-NEXT LOOP"
300 PRINT "  LOOPS PASSED."

310 PRINT "  TESTING NESTED LOOPS..."
315 S=0
316 FOR I=1 TO 5
317   FOR J=1 TO 5
318     S=S+1
319   NEXT J
320 NEXT I
321 IF (S)<>(25) THEN PRINT "ERROR: NESTED LOOP"
322 PRINT "  NESTED LOOPS PASSED."

330 PRINT "  TESTING GOSUB AND RETURN..."
340 GOSUB 500
350 IF (R)<>(42) THEN PRINT "ERROR: GOSUB/RETURN"
360 PRINT "  GOSUB AND RETURN PASSED."

370 PRINT "  TESTING PRINT SPACING..."
380 PRINT "  ..."; ",,";"...X--- SEMICOLON"
390 PRINT "  ...","X--- COMMA"
400 PRINT SPC(5); ",,,,,X--- SPACED"
410 PRINT TAB(10); "X--- TABBED"
420 PRINT "      ALL X MUST ALIGN"


430 PRINT "  ALL TESTS COMPLETED!"
440 END

500 R=42: RETURN
